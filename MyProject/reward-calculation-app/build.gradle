plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version "3.4.5"
    id 'com.palantir.docker' version '0.36.0'
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(21)) // Use JDK 21
	}
}

compileJava {
	options.encoding = "UTF-8"
}

compileTestJava {
	options.encoding = "UTF-8"
}

repositories {
	mavenCentral()
}

ext {
	set("springBootVersion", "3.4.5")
}

dependencies {

	implementation('mysql:mysql-connector-java:8.0.33')
	implementation('org.hibernate:hibernate-core:6.5.0.Final')
	implementation ('org.liquibase:liquibase-core:4.27.0')
	implementation('com.h2database:h2:2.2.224')

	implementation("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
	implementation("org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion")
    implementation "org.springframework.boot:spring-boot-starter-cache:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-aop:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-data-redis:$springBootVersion"

    // Resilience4j
    implementation 'io.github.resilience4j:resilience4j-circuitbreaker:2.3.0'
    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.3.0'
// Actuator + Micrometer + Prometheus (для мониторинга CircuitBreaker)
    implementation "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    implementation "io.micrometer:micrometer-registry-prometheus:1.11.4" // или актуальная версия
    implementation 'io.github.resilience4j:resilience4j-micrometer:2.3.0'
    compileOnly('org.projectlombok:lombok:1.18.32')
	annotationProcessor('org.projectlombok:lombok:1.18.32')

	testCompileOnly('org.projectlombok:lombok:1.18.32')
	testAnnotationProcessor('org.projectlombok:lombok:1.18.32')

	testImplementation("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
}

test {
	useJUnitPlatform()
}

clean.doFirst {
	delete "${projectDir}/logs/"
	println "${projectDir}/logs/"

	delete "${projectDir}/out/"
	println "${projectDir}/out/"

	delete "${projectDir}/build/"
	println "${projectDir}/build/"
}

task createLogsDir {
	doLast {
		def logsDir = file("logs")
		if (!logsDir.exists()) {
			logsDir.mkdirs()
			println "Created logs directory: ${logsDir}"
		}
	}
}

jar {
	enabled = false
}

bootJar {
	enabled = true
	archiveBaseName = 'reward-calculation-app'
	archiveVersion = '1.0.1'
	dependsOn createLogsDir
}

build {
	dependsOn createLogsDir
}
dockerPrepare {
	dependsOn bootJar
}
docker {
	dependsOn bootJar
	name "reward-calculation-app:1.0.1"
	files "build/libs/reward-calculation-app-1.0.1.jar"
	files 'wait-for-it.sh'
	buildArgs(['JAR_FILE': "reward-calculation-app-1.0.1.jar"])
}
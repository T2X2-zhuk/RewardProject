plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version "3.4.5"
    id 'com.palantir.docker' version '0.36.0'
}

java {
	toolchain {
		languageVersion.set(JavaLanguageVersion.of(21)) // Use JDK 21
	}
}

compileJava {
	options.encoding = "UTF-8"
}

compileTestJava {
	options.encoding = "UTF-8"
}

repositories {
	mavenCentral()
}

ext {
	set("springBootVersion", "3.4.5")
}

dependencies {

    // JDBC драйвер для Liquibase
    implementation 'mysql:mysql-connector-java:8.0.33'

    // AOP
    implementation "org.springframework.boot:spring-boot-starter-aop:${springBootVersion}"

    // Reactive web
    implementation "org.springframework.boot:spring-boot-starter-webflux:${springBootVersion}"

    // R2DBC (reactive DB access)
    implementation "org.springframework.boot:spring-boot-starter-data-r2dbc:${springBootVersion}"
    implementation "io.asyncer:r2dbc-mysql:1.2.0" // reactive mysql driver

    // Reactive Redis
    implementation "org.springframework.boot:spring-boot-starter-data-redis-reactive:${springBootVersion}"
    implementation 'org.redisson:redisson-spring-boot-starter:3.29.0'

    // Micrometer + OpenTelemetry
    implementation "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    implementation 'io.micrometer:micrometer-registry-prometheus:1.13.3'
    implementation "io.micrometer:micrometer-tracing-bridge-otel:1.3.4"
    implementation "io.opentelemetry:opentelemetry-exporter-otlp:1.39.0"
    implementation "io.opentelemetry:opentelemetry-sdk-extension-autoconfigure:1.39.0"
// --- Kafka ---
    implementation "org.springframework.kafka:spring-kafka:3.3.10"
    testImplementation "org.springframework.kafka:spring-kafka-test:3.3.10"

    // Liquibase
    implementation 'org.liquibase:liquibase-core:4.27.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    testCompileOnly 'org.projectlombok:lombok:1.18.32'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'

    // Тесты
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "io.projectreactor:reactor-test:3.6.13"

}

test {
	useJUnitPlatform()
}

clean.doFirst {
	delete "${projectDir}/logs/"
	println "${projectDir}/logs/"

	delete "${projectDir}/out/"
	println "${projectDir}/out/"

	delete "${projectDir}/build/"
	println "${projectDir}/build/"
}

task createLogsDirs {
    doLast {
        def baseDirs = [
                "logs/server1",
                "logs/server2"
        ]

        baseDirs.each { dirPath ->
            def dir = file(dirPath)
            if (!dir.exists()) {
                dir.mkdirs()
                println "Created directory: ${dir}"
            }
        }
    }
}

jar {
	enabled = false
}

bootJar {
	enabled = true
	archiveBaseName = 'reward-payment-app'
	archiveVersion = '1.0.1'
	dependsOn createLogsDirs
}

build {
	dependsOn createLogsDirs
}

dockerPrepare {
	dependsOn bootJar
}

docker {
	dependsOn bootJar
	name "reward-payment-app:1.0.1"
	files "build/libs/reward-payment-app-1.0.1.jar"
	buildArgs(['JAR_FILE': "reward-payment-app-1.0.1.jar"])
}